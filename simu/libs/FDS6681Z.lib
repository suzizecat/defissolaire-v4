.SUBCKT FDS6681Z 2 1 3
*Nom Temp 25 deg C
Dbody 5 7 DbodyMOD
Dbreak 7 11 DbreakMOD
Lgate 1 9 1.62e-9
Ldrain 2 5 0.1e-9
Lsource 3 7 0.51e-9
RLgate 1 9 16.2
RLdrain 2 5 1.0
RLsource 3 7 5.1
Rgate 9 6 12
It 7 17 1
Ebreak 5 11 17 7 -32.8
Rbreak 17 7 RbreakMOD 1
.MODEL RbreakMOD RES (TC1=0.74e-3 TC2=-0.4e-6)
.MODEL DbodyMOD D (IS=3.0e-12 n=1 RS=7.3e-3 TRS1=2e-3 TRS2=1e-6
+ CJO=24e-10 M=0.9 TT=0.1e-9 XTI=3)
.MODEL DbreakMOD D (RS=100e-3 TRS1=1e-3 TRS2=1e-6)
Rsource 77 7 1.50e-3
Rdrain 5 16 RdrainMOD 1.88e-3
.MODEL RdrainMOD RES (TC1=5.4e-3 TC2=5.8e-6)
M_BSIM3 16 6 77 77 Bsim3 W=18.72 L=1.1e-6 NRS=0 NRD=0
.MODEL Bsim3 PMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=550e-10 ;Oxide thickness
+ XJ=0.3e-6 ;Channel depth
+ NCH=1.36e17 ;Channel concentration
*Channel Current
+ U0=270 VSAT=500000 DROUT=1.8
+ DELTA=0.35 PSCBE2=0.00001 RSH=1.44e-3
*Threshold voltage
+ VTH0=-1.97
*Sub-threshold characteristics
+ VOFF=-0.16 NFACTOR=1
*Junction diodes and Capacitance
+ LINT=0.3e-6 DLC=0.3e-6
+ CGSO=75e-12 CGSL=0 CGDO=15e-12 CGDL=373e-12
+ CJ=0 CF=0 CKAPPA=1.2
* Temperature parameters
+ KT1=-1.35 KT2=0 UA1=3.8e-9
+ NJ=10)
* Rev.B		8/3/2015
* SP
.ENDS FDS6681Z

